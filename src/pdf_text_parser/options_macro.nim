##[ options_macro.nim

this is a parsing command line macro for nim-lang.

License: MIT, see LICENSE
]##
import tables
import macros


type
  tuple_parses = tuple[short: char,
                       long, fallback: string]


proc append(tbl: var Table[string, seq[string]], p, value: string): void =
    if tbl.contains(p):
        tbl[p] = tbl[p] & @[value]
    else:
        tbl[p] = @[value]


proc parse(args: seq[string], defs: Table[string, tuple_parses]
           ): Table[string, seq[string]] =
            # tuple[s: char, l, f: string]]
    ##[ parses command line options to the table.
    ]##
    var nxt = ""
    for arg in args:
        if nxt != "":
            append(result, nxt, arg)
            nxt = ""
            continue
        var f_proc = false
        for key, (s, l, f) in defs:
            if arg == '-' & s:
                discard
            elif arg == l:
                discard
            else:
                continue
            f_proc = true
            if len(f) < 1:
                nxt = key; continue
            append(result, key, f)
        if not f_proc:
            append(result, "", arg)


macro parse_all*(obj: untyped, args: typed, parsers: varargs[untyped]): void =
    ##[ this macro will be expanded to:

        ```
        let definitions = {a: b, c: d, ...}.toTable()
        let prms = parse(args, definitions)
        result.(a.sym) = a.fn(getOrDefault(prms, b.id, @[]))
        result.(b.sym) = b.fn(getOrDefault(prms, b.id, @[]))
        result.(c.sym) = c.fn(getOrDefault(prms, c.id, @[]))
        ...
        ```
    ]##
    template short(a: NimNode): NimNode = a[0]
    template long(a: NimNode): NimNode = a[1]
    template fallback(a: NimNode): NimNode = a[2]
    template fn(a: NimNode): NimNode = a[3]
    template sym(a: NimNode): NimNode = a[4]
    template fn_str(a: NimNode): NimNode = newLit(strVal(a))

    var (defs, prms) = (ident"definitions", ident"prms")

    # make the table: {"fn1": (short:' ', long:"", fallback:""),
    #                  "fn2": (short:' ', long:"", fallback:""), ...}
    var tuples = newTree(nnkTableConstr)
    for i in parsers:
        tuples.add(newColonExpr(
            fn_str(i.fn),
            newNimNode(nnkTupleConstr
                       ).add(newColonExpr(ident("short"), i.short)
                       ).add(newColonExpr(ident("long"), i.long)
                       ).add(newColonExpr(ident("fallback"), i.fallback)
                       )
        ))
    result = newStmtList()
    # let definitions = {...}.toTable()
    result.add(newLetStmt(defs, newCall(bindSym"toTable", tuples)))
    # let prms = parse(args, definitions)
    result.add(newLetStmt(prms, newCall(bindSym"parse", args, defs)))
    for i in parsers:
        # fn(prms.getOrDefault("fn", @[]))
        let cl = newCall(i.fn,
                newCall(bindSym"getOrDefault",
                        prms, fn_str(i.fn), newLit(newSeq[string]())
                )
            )
        # obj.sym = fn(...)
        result.add(newAssignment(newDotExpr(obj, i.sym), cl))


